task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
    configureMaven(project)
    configureGitRevision(project, rootDir)
}

static def configureMaven(Project project) {
    project.apply plugin: 'maven'
    project.group = 'com.github.michiruf'
}

static def configureGitRevision(Project project, File rootDir) {
    String latestTag = ('git describe --abbrev=0')
            .execute((List) null, rootDir)
            .text
            .trim()
    String module = getRelativeProjectPath(project, rootDir)
    String commitCount = ('git rev-list --count HEAD ' + module)
            .execute((List) null, rootDir)
            .text
            .trim()
    project.version = latestTag + '.' + commitCount
}

static def getRelativeProjectPath(Project project, File relativeTo) {
    String relativePath = project.getProjectDir().getCanonicalPath()
            .replace(relativeTo.getCanonicalPath(), '')
            .replace('\\', '/')
    if (relativePath.startsWith('/')) {
        relativePath = relativePath.substring(1)
    }
    return relativePath
}
